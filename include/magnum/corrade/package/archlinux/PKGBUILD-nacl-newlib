# Author: mosra <mosra@centrum.cz>
pkgname=nacl-corrade
pkgver=dev.newlib
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (NaCl newlib version)"
arch=('any')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('nacl-sdk' 'cmake' 'ninja')
options=('!buildflags' '!strip' 'staticlibs')

_rootdir=$startdir/../../

build() {
    # Build 32bit
    mkdir -p "$_rootdir/build-nacl-newlib-x86-32"
    cd "$_rootdir/build-nacl-newlib-x86-32"

    cmake .. \
        -DCMAKE_MODULE_PATH="$_rootdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/NaCl-newlib-x86-32.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DGCC44_COMPATIBILITY=ON \
        -DBUILD_STATIC=ON \
        -DLIB_SUFFIX=/32 \
        -G Ninja
    ninja

    # Build 64bit
    mkdir -p "$_rootdir/build-nacl-newlib-x86-64"
    cd "$_rootdir/build-nacl-newlib-x86-64"

    cmake .. \
        -DCMAKE_MODULE_PATH="$_rootdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/NaCl-newlib-x86-64.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DGCC44_COMPATIBILITY=ON \
        -DBUILD_STATIC=ON \
        -G Ninja
    ninja
}

package() {
    # Install 32bit
    cd "$_rootdir/build-nacl-newlib-x86-32"
    DESTDIR="$pkgdir/" ninja install/strip

    # Install 64bit (the headers will be overwritten, but they are (and should
    # be) the same for both versions
    cd "$_rootdir/build-nacl-newlib-x86-64"
    DESTDIR="$pkgdir/" ninja install/strip
}
